buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.1.RELEASE")
    classpath('org.springframework:springloaded:1.2.5.RELEASE')
  }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'spring-boot'

sourceSets {
  main {
    resources {
      srcDir 'src/main/web'
    }
  }
}

jar {
  baseName = 'basearch'
  version =  '0.1.0'
}

repositories {
  mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
  options.incremental = true
}

configurations {
  weave
  metamodel
}

dependencies {
  compile("org.springframework.boot:spring-boot-starter-web") {
    exclude module: "spring-boot-starter-tomcat"
  }
  compile("org.springframework.boot:spring-boot-starter-undertow")
  compile("org.springframework.boot:spring-boot-starter-actuator")
  compile("org.springframework.boot:spring-boot-starter-security")
  compile("org.springframework.boot:spring-boot-starter-thymeleaf")
  compile("org.springframework.boot:spring-boot-starter-jdbc")

  compile 'javax.inject:javax.inject:1'
  compile 'org.springframework:spring-orm'
  compile 'org.eclipse.persistence:javax.persistence'
  compile 'org.eclipse.persistence:org.eclipse.persistence.jpa'
  compile 'org.thymeleaf.extras:thymeleaf-extras-springsecurity4'

  runtime("org.hsqldb:hsqldb")

  testCompile("org.springframework.boot:spring-boot-starter-test")

  weave 'org.eclipse.persistence:javax.persistence'
  weave 'org.eclipse.persistence:org.eclipse.persistence.jpa'

  metamodel 'org.eclipse.persistence:javax.persistence'
  metamodel 'org.eclipse.persistence:org.eclipse.persistence.jpa'
  metamodel 'org.eclipse.persistence:org.eclipse.persistence.jpa.modelgen.processor'
  metamodel 'org.springframework.security:spring-security-core'
}

configurations.all {
  resolutionStrategy.dependencySubstitution {
    substitute module('org.springframework:spring-orm') with module('org.springframework:spring-orm:4.2.4.RELEASE')
    substitute module('org.eclipse.persistence:javax.persistence') with module('org.eclipse.persistence:javax.persistence:2.1.0')
    substitute module('org.eclipse.persistence:org.eclipse.persistence.jpa') with module('org.eclipse.persistence:org.eclipse.persistence.jpa:2.6.2')
    substitute module('org.eclipse.persistence:org.eclipse.persistence.jpa.modelgen.processor') with module('org.eclipse.persistence:org.eclipse.persistence.jpa.modelgen.processor:2.6.2')
    substitute module('org.thymeleaf.extras:thymeleaf-extras-springsecurity4') with module('org.thymeleaf.extras:thymeleaf-extras-springsecurity4:2.1.2.RELEASE')
  }
}

// local run marker task
task start(dependsOn: bootRun) {
	outputs.upToDateWhen { false }
}

// project defs
def entitiesBasePackage = 'basearch.model'


compileJava {
   destinationDir file("$buildDir/compiled-classes")
}



// environment dependent resources

task processDevelopmentResources(type: Copy, dependsOn: processResources) {
   from 'env/dev/resources'
   into processResources.destinationDir
   onlyIf { !gradle.taskGraph.hasTask(release) }
}
task processDevelopmentWebInf(type: Sync, dependsOn: processResources) {
   from 'env/dev/web-inf'
   into "$buildDir/env-dependent-webcontent/WEB-INF"
   onlyIf { !gradle.taskGraph.hasTask(release) }
}
task processReleaseResources(type: Copy, dependsOn: processResources) {
   from 'env/prod/resources'
   into processResources.destinationDir
   onlyIf { gradle.taskGraph.hasTask(release) }
}
task processReleaseWebInf(type: Sync, dependsOn: processResources) {
   from 'env/prod/web-inf'
   into "$buildDir/env-dependent-webcontent/WEB-INF"
   onlyIf { gradle.taskGraph.hasTask(release) }
}


// development resources

task processDevelopmentSqls(type: Copy, dependsOn: processResources) {
   from "$rootDir/sql"
   into processResources.destinationDir
}
classes.dependsOn processDevelopmentSqls


// jpa weaving

task copyNonPersistentClasses(type: Copy, dependsOn: compileJava) {
  from "$buildDir/compiled-classes"
  into sourceSets.main.output.classesDir
  exclude '**/' + entitiesBasePackage.replaceAll('\\.','/') + '/**'
  includeEmptyDirs = false
}
task copyPersistentClasses(type: Copy, dependsOn: compileJava) {
  from "$buildDir/compiled-classes"
  into "$buildDir/unwoven-persistent-classes"
  include '**/' + entitiesBasePackage.replaceAll('\\.','/') + '/**'
  includeEmptyDirs = false
}
task weaveJpaEntities(type: JavaExec) {
  main = 'org.eclipse.persistence.tools.weaving.jpa.StaticWeave'
  classpath configurations.weave.incoming.files
  args '-persistenceinfo'
  args processResources.destinationDir.absolutePath
  args '-classpath'
  args configurations.compile.incoming.files.asPath
  args '-loglevel'
  args 'INFO'
  args copyPersistentClasses.destinationDir.absolutePath
  args sourceSets.main.output.classesDir.absolutePath

  inputs.files fileTree(copyPersistentClasses.destinationDir),fileTree(processResources.destinationDir).matching({pattern -> pattern.include('**/META-INF/persistence.xml')})
  outputs.dir sourceSets.main.output.classesDir
}
weaveJpaEntities.dependsOn copyPersistentClasses,processDevelopmentResources,processDevelopmentWebInf,processDevelopmentSqls,processReleaseResources,processReleaseWebInf
classes.dependsOn weaveJpaEntities


// test resources

task processTestSqls(type: Copy, dependsOn: processTestResources) {
   from "$rootDir/sql"
   into processTestResources.destinationDir
}
testClasses.dependsOn processTestSqls



// metamodel generation

task deleteMetamodel(type: Delete) {
  delete sourceSets.main.java.matching({pattern -> pattern.include('**/' + entitiesBasePackage.replaceAll('\\.','/') + '/**/*_.java')})
}
task generateMetamodel(type: JavaCompile, dependsOn: deleteMetamodel) {
  classpath = configurations.metamodel.incoming.files
  source sourceSets.main.java.matching({pattern -> pattern.include('**/' + entitiesBasePackage.replaceAll('\\.','/') + '/**')})
  destinationDir = sourceSets.main.java.getSrcDirs().iterator().next()
  options.compilerArgs << '-proc:only'
  options.compilerArgs << '-Aeclipselink.persistencexml=' + "$projectDir/env/dev/resources/META-INF/persistence.xml"
}




// Gradle wrapper task
// Only used when in need to reinstall the wrapper
task wrapper(type: Wrapper) {
  gradleVersion = '2.10'
}